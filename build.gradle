task wrapper(type: Wrapper) {
    gradleVersion = '1.9'
    // это максимальная версия, с которой без ошибок работает gradle plugin в IDEA
}

def commonProject = project(':common')
def serverProject = project(':server')
def serviceProject = project(':service')
def webProject = project(':web')
def tomcatPath = 'tomcat'
def tomcatWebApp = tomcatPath + '/webapps'
def warFileName = 'template'
def webWarFileName = 'template-web'
// VERSIONS
def spring_version = '4.2.2.RELEASE'
def spring_security_version = '4.0.3.RELEASE'
def hibernate_version = '4.3.8.Final'
def jackson_version = '1.9.13'
def fasterxml_jackson_version = '2.6.3'

//println 'Common project dir: ' + commonProject.projectDir

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    version = '1.0'
    jar {
        manifest {
            attributes 'Implementation-Title': 'Nikolay Product',
                    'Implementation-Version': version
        }
    }
    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        testCompile 'junit:junit:4.11'
        compile 'javax:javaee-api:6.0'
        compile 'log4j:log4j:1.2.17'
    }
}

configure([commonProject]) {
    dependencies {
        compile 'org.springframework:spring-web:' + spring_version
        compile 'org.springframework:spring-tx:' + spring_version
        compile 'org.springframework:spring-orm:' + spring_version
        compile 'org.hibernate:hibernate-entitymanager:' + hibernate_version
        compile 'org.hibernate:hibernate-core:' + hibernate_version
        compile 'org.springframework.security:spring-security-core:' + spring_security_version
        compile 'mysql:mysql-connector-java:5.1.9'
        compile 'commons-dbcp:commons-dbcp:1.4'
    }
}


configure([serverProject]) {
    dependencies {
        compile commonProject
    }
}

configure([serviceProject]) {
    apply plugin: 'war'
    dependencies {
        compile commonProject
        compile serverProject
        providedCompile "javax.servlet:servlet-api:2.5"
        providedCompile 'log4j:log4j:1.2.17'
        compile 'org.springframework:spring-webmvc:' + spring_version
        compile 'org.springframework:spring-web:' + spring_version
        compile 'org.springframework.security:spring-security-web:' + spring_security_version
        compile 'org.springframework.security:spring-security-config:' + spring_security_version
        compile 'org.codehaus.jackson:jackson-mapper-asl:' + jackson_version
        compile 'com.fasterxml.jackson.core:jackson-core:' + fasterxml_jackson_version
        compile 'com.fasterxml.jackson.core:jackson-databind:' + fasterxml_jackson_version
        compile 'javax.inject:javax.inject:1'
    }
    war {
        archiveName = warFileName + ".war"
    }
}

configure([webProject]) {
    apply plugin: 'war'
    dependencies {
        providedCompile "javax.servlet:servlet-api:2.5"
        providedCompile 'log4j:log4j:1.2.17'
    }
    war {
        archiveName = webWarFileName + ".war"
    }
}


task cleanTomcatDeployDir(type: org.gradle.api.tasks.Delete) {
    delete tomcatWebApp + '/' + warFileName
    delete tomcatWebApp + '/' + warFileName + '.war'
    delete tomcatWebApp + '/' + webWarFileName
    delete tomcatWebApp + '/' + webWarFileName + '.war'
}

task createAndCopyWar(type: Copy, dependsOn: [serviceProject.build, webProject.build, cleanTomcatDeployDir]) {
    from 'src/service/build/libs/' + warFileName + '.war', 'src/web/build/libs/' + webWarFileName + '.war'
    into tomcatWebApp
}

configurations {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    liquibase
}

dependencies {
    liquibase 'org.liquibase:liquibase-core:3.4.1'
}

task liquibase_Update(type: JavaExec) {
    group = "Liquibase"
    classpath configurations.liquibase
    main = "liquibase.integration.commandline.Main"
    workingDir = 'src/db'

    args "--changeLogFile=liquibase/update.xml"
    args "--defaultsFile=liquibase.properties"
    args "--logLevel=info"

    args "update"
}